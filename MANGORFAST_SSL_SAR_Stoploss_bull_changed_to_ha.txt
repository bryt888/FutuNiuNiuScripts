{HMA}
PV:=0.02;
W:=C*EMA((O+L+H+C),2)/EMA((O+L+H+C),8);
N1:=60;
BBMC:=WMA(2*WMA(W,N1/2)-WMA(W,N1),ROUND(SQRT(N1)));
RANGEMA:=EMA(TR,N1);
UPPERK:=BBMC+RANGEMA*0.2;
LOWERK:=BBMC-RANGEMA*0.2;

BBMC,COLORGRAY,LINETHICK4;
IF(W>UPPERK,BBMC,DRAWNULL),COLORBLUE,LINETHICK4;
IF(W<LOWERK,BBMC,DRAWNULL),COLORMAGENTA,LINETHICK4;

FLAG1:=W>UPPERK AND NOT(REF(W>UPPERK,1));
FLAG2:=W<LOWERK AND NOT(REF(W<LOWERK,1));
IF(FLAG1,BBMC,DRAWNULL),COLORBLUE,POINTDOT,LINETHICK5;
IF(FLAG2,BBMC,DRAWNULL),COLORMAGENTA,POINTDOT,LINETHICK5;

N2:=15;
EXITHIGH:=WMA(2*WMA(HIGH,N2/2)-WMA(HIGH,N2),ROUND(SQRT(N2)));
EXITLOW:=WMA(2*WMA(LOW,N2/2)-WMA(LOW,N2),ROUND(SQRT(N2)));
HLV3:=IF(W>EXITHIGH,1,IF(W<EXITLOW,-1,0));
LAST_PERIOD:=BARSLAST(HLV3);
HLV4:=IF(HLV3=0,REF(HLV3,LAST_PERIOD),HLV3);
SSLEXIT:=IF(HLV4<0,EXITHIGH,EXITLOW);
{TEXTA:=DRAWNUMBER(CURRBARSCOUNT=1,BBMC,ROUND2(BBMC,1)),COLORWHITE;}{SHOW DATA}

{COLOR4}
P1:=9; 
P2:=3; 
P3:=3;
RSV:=(CLOSE-LLV(LOW,P1))/(HHV(HIGH,P1)-LLV(LOW,P1))*100; 
K:=SMA(RSV,P2,1); 
D:=SMA(K,P3,1); 
J:=3*K-2*D;
持股:J>50,COLOR0003FF,NODRAW;
空倉:J<50,COLOR69FFEE,NODRAW;
買入:CROSS(J,50) ,COLORFF0000,NODRAW;
賣出:CROSS(50,J) ,COLORF1FA00,NODRAW;

{TSI}
LONG:= 25;
SHORT:= 4;
SIGNAL:=14;
MTM:=CLOSE-REF(CLOSE,1);
TEMP1:=EMA(MTM,25);
TEMP2:=ABS(MTM);
TSI1:=(EMA(TEMP1,13)/EMA(EMA(TEMP2,25),13)*100);
TSI:=(EMA(TEMP1,13)/EMA(EMA(TEMP2,25),13)*100);
SIG:=EMA(TSI1,13);

{ENERGY BAR}
VOLUME:VOL,VOLSTICK,NODRAW;
SHORT1:=12;
LONG1:=26;
MID:=9;
DIF:=EMA(CLOSE,SHORT1)-EMA(CLOSE,LONG1);
DEA:=EMA(DIF,MID);
Q:=9;
M1:=3;
M2:=3;
RSV1:=(CLOSE-LLV(LOW,Q))/(HHV(HIGH,Q)-LLV(LOW,Q))*100;
K1:=SMA(RSV1,M1,1);
D1:=SMA(K1,M2,1);
J1:=3*K1-2*D1;
VA:=IF(CLOSE>REF(CLOSE,1),VOL,IF(CLOSE<REF(CLOSE,1),-VOL,0));
OBV1:=SUM(VA,TOTALBARSCOUNT);
OBV2:=EMA(OBV1,3)-MA(OBV1,9);
OBV3:=EMA(IF(OBV2>0,OBV2,0),3);
MAC3:=MA(C,3);
COND:=OBV3>REF(OBV3,1) AND MAC3>REF(MAC3,1);


{HA}
HACLOSE:((O+H+L+C)/4),NODRAW;
{HAOPEN:((REF(O,1)+REF(C,1))/2),NODRAW;}
HAOPEN:DMA(REF(HACLOSE,1),0.5),NODRAW;
HAHIGH:(MAX(MAX(H,HAOPEN),HACLOSE)),NODRAW;
HALOW:(MIN(MIN(L,HAOPEN),HACLOSE)),NODRAW;
{FIRST SMOOTH}
HACLOSE2:EMA(HACLOSE,10),NODRAW;
HAOPEN2:EMA(DMA(REF(HACLOSE,1),0.5),10),NODRAW;
HAHIGH2:EMA((MAX(MAX(H,HAOPEN),HACLOSE)),10),NODRAW;
HALOW2:EMA((MIN(MIN(L,HAOPEN),HACLOSE)),10),NODRAW;
{SHOW HA?}
{STICKLINE(HACLOSE2>HAOPEN2,HAOPEN2,HACLOSE2,0.8,0),COLORBLUE;
STICKLINE(HACLOSE2>HAOPEN2,HALOW2,HAHIGH2,0.1,0),COLORBLUE;
STICKLINE(HACLOSE2<=HAOPEN2,HAOPEN2,HACLOSE2,0.8,0),COLORMAGENTA;
STICKLINE(HACLOSE2<=HAOPEN2,HALOW2,HAHIGH2,0.1,0),COLORMAGENTA;}
SUN:=HACLOSE2>HAOPEN2
DARK:=HACLOSE2<=HAOPEN2

{SUPPORT AND RES}
R:=10;
A1:=REF(C,R)=HHV(C,R*2);
B1:=FILTER(A1,R);
C1:=BACKSET(B1,R+1);
HD:=FILTER(C1,R);
BOX_TOP:=REF(H,BARSLAST(HD));
_W:=(HHV(C, 200)-LLV(C, 200)) *0.002
STICKLINE(C,BOX_TOP,BOX_TOP+_W,6,2),LINETHICK2,COLORRED;
A2:=REF(C,R)=LLV(C,R*2);
B2:=FILTER(A2,R);
C2:=BACKSET(B2,R+1);
LD:=FILTER(C2,R);
BOX_BOT:=REF(L,BARSLAST(LD));
STICKLINE(C,BOX_BOT,BOX_BOT-_W,6,2),LINETHICK2,COLORGREEN;

{SAR}
BB:=ROUND(SAR(4,2,20));

{BUY SET}
SETEB:=DIF<DEA AND J<D 
BUY1:=NOT(PERIOD=0) AND CROSS(TSI,SIG)AND NOT(SETEB) AND SUN AND (持股 OR 買入)  AND C>UPPERK AND BBMC>REF(BBMC,1) AND TSI<30 AND ABS(C-O)<30;
BUY2:=TSI>SIG AND NOT(SETEB) AND SUN  AND (持股 OR 買入)  AND C>UPPERK AND BBMC>REF(BBMC,1) AND TSI<30;
DRAWICON(BUY1,L*0.9998,1),COLORFFFF00,LINETHICK9
DRAWTEXT(BUY2,L,'●'),COLORFFFF00,LINETHICK1;


{SELL SET}
SELL1:=NOT(PERIOD=0) AND CROSS(SIG,TSI) AND SETEB AND DARK AND (空倉 OR 賣出) AND C<LOWERK AND BBMC<REF(BBMC,1) AND TSI>-30 AND ABS(C-O)<30
SELL2:=SIG>TSI AND SETEB AND DARK AND (空倉 OR 賣出) AND C<LOWERK AND BBMC<REF(BBMC,1) AND TSI>-30
DRAWICON(SELL1,H*1.0001,2),COLORFFFF00,LINETHICK9
DRAWTEXT(SELL2,H,'●'),COLORWHITE,LINETHICK1;
